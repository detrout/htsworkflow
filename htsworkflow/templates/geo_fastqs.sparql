PREFIX libraryOntology: <http://jumpgate.caltech.edu/wiki/LibraryOntology#>
PREFIX submissionOntology: <http://jumpgate.caltech.edu/wiki/UcscSubmissionOntology#>
PREFIX ucscDaf: <http://jumpgate.caltech.edu/wiki/UcscDaf#>
PREFIX ncbiTaxon: <http://www.ncbi.nlm.nih.gov/Taxonomy/Browser/wwwtax.cgi?id=>
PREFIX geoSoft: <http://www.ncbi.nlm.nih.gov/geo/info/soft2.html#>
PREFIX dc: <http://purl.org/dc/elements/1.1/>

select distinct ?library ?filename, ?md5sum, ?file_type ?file_type_label ?flowcell_id ?read_length ?flowcell_type ?image_software ?image_version ?basecall_software ?basecall_version ?sequencer_model
WHERE {
  <{{submission}}> ucscDaf:has_file ?file ;
                   submissionOntology:library ?library ;
                   a submissionOntology:submission .

  ?file ucscDaf:filename ?filename ;
        ucscDaf:md5sum ?md5sum ;
        libraryOntology:flowcell_id ?flowcell_id ;
        a ?file_type .
        
  ?file_type a <{{file_class}}> ;
             geoSoft:fileTypeLabel ?file_type_label .

  ?flowcell libraryOntology:flowcell_id ?flowcell_id ;
            libraryOntology:read_length ?read_length ;
            libraryOntology:flowcell_type ?flowcell_type ;
  OPTIONAL { ?flowcell libraryOntology:image_software ?image_software ;
                       libraryOntology:image_version ?image_version . }
  OPTIONAL {?flowcell  libraryOntology:basecall_software ?basecall_software ;
                       libraryOntology:basecall_version ?basecall_version . }
  OPTIONAL {?flowcell  libraryOntology:sequenced_by ?sequencer .
             ?sequencer libraryOntology:sequencer_model ?sequencer_model . }
}

